#!/bin/sh
#
# Parse the output of chipsec and write a luv-test-manager compatible
# log to stdout.

awk ' /\[\+\] imported/ {
	test_strs[$3] = 1;
}

/\[\*\] SKIPPED/ {
	if (test_strs[$3]) {
		printf("1.0 chipsec %s RESULT 0 0 0 0 1\r\n", $3);
	}
}

/^ERROR/ {
	if (test_strs[$2]) {
		printf("1.0 chipsec %s RESULT 0 0 0 1 0\r\n", $2);
	}
}

/\[\-\] FAILED/ {
	if (test_strs[$3]) {
		printf("1.0 chipsec %s RESULT 0 1 0 0 0\r\n", $3);
	}
}

/\[\+\] PASSED/ {
	if (test_strs[$3]) {
		printf("1.0 chipsec %s RESULT 1 0 0 0 0\r\n", $3);
	}
}'
